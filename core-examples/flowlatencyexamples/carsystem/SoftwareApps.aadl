package SoftwareApps
public
with Sensors, Actuators, CCThreads;

  system CruiseControl
    features
      cc_wrs: feature group inverse of Sensors::wrs_cc_type;
      cc_bp: feature group inverse of Sensors::bp_cc_type;
      cc_ui: feature group;
      cc_ta: feature group inverse of Actuators::ta_cc_type;
      cc_ud: feature group;
    flows
      Flow_W_T: flow path cc_wrs -> cc_ta{
        Latency => 20 Ms .. 20 Ms;
        };
      Flow_B_T: flow path cc_bp -> cc_ta{
        Latency => 15 Ms .. 15 Ms;
        };
  end CruiseControl;

  system implementation CruiseControl.impl
    subcomponents
      cc_process: process CCThreads::CruiseControlProcess.impl;
    connections
      d1: port cc_wrs.wheel_pulses -> cc_process.wheel_pulses;
      d2: port cc_bp.brake_pedal -> cc_process.brake_pedal;
      d3: port cc_process.throttle_setting -> cc_ta.throttle_setting;
    flows
      Flow_W_T: flow path cc_wrs.wheel_pulses -> d1 -> cc_process.Flow_W_T
         -> d3 -> cc_ta.throttle_setting;
      Flow_B_T: flow path cc_bp.brake_pedal -> d2 -> cc_process.Flow_B_T
         -> d3 -> cc_ta.throttle_setting;
  end CruiseControl.impl;
  
end SoftwareApps;
