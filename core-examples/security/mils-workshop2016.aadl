package me
public

abstract sender
features
	dataout : out data port;
end sender;

abstract merger
features
	datain_secret : in data port;
	datain_unclassified : in data port;
	dataout_unclassified1 : out data port;
	dataout_unclassified2 : out data port;
end merger;


abstract receiver
features
	datain : in data port;
end receiver;


system functional

end functional;

system implementation functional.impl
subcomponents
	sender_secret : abstract sender;
	sender_unclassified : abstract sender;
	merger : abstract merger;
	receiver_secret : abstract receiver;
	receiver_unclassified : abstract receiver;
connections
	c0 : port sender_secret.dataout -> merger.datain_secret;
	c1 : port sender_unclassified.dataout -> merger.datain_unclassified;
	c2 : port merger.dataout_unclassified1 -> receiver_secret.datain;
	c3 : port merger.dataout_unclassified2 -> receiver_unclassified.datain;
end functional.impl;

virtual processor partition
end partition;

processor separation_kernel
end separation_kernel;

processor implementation separation_kernel.impl
subcomponents
	partition_secret : virtual processor partition;
	partition_mls : virtual processor partition;
	partition_unclassified : virtual processor partition;
end separation_kernel.impl;

system integration extends functional
end integration;

system implementation integration.impl extends functional.impl
subcomponents
	separation_kernel : processor separation_kernel.impl;
properties
	Actual_Processor_Binding => (reference (separation_kernel.partition_secret)) applies to sender_secret;
	Actual_Processor_Binding => (reference (separation_kernel.partition_secret)) applies to receiver_secret;
	Actual_Processor_Binding => (reference (separation_kernel.partition_unclassified)) applies to sender_unclassified;
	Actual_Processor_Binding => (reference (separation_kernel.partition_unclassified)) applies to receiver_unclassified;
	Actual_Processor_Binding => (reference (separation_kernel.partition_mls)) applies to merger;
end integration.impl;

end me;
