package aircraft
public
with GenericFunctions;
with WBSPS,EMV2,ARP4761,WheelBrake,Throttle,Electrical,Hydraulic;

system Aircraft
end Aircraft;

system implementation Aircraft.basic
subcomponents
	decelerate : system DecelerateAircraft.basic;
	electrical : system Electrical::Electrical.basic;
	hydraulic  : system Hydraulic::Hydraulic.basic;
	alert      : system GenericFunctions::Generic.basic;
	steering   : system GenericFunctions::Generic.basic;
	status     : system GenericFunctions::Generic.basic;
connections
	c1 : feature electrical.isFailing -> decelerate.fromElectrical;
	c2 : feature hydraulic.isFailing -> decelerate.fromHydraulic;
	c3 : feature alert.isFailing -> decelerate.fromAlert;
	c4 : feature steering.isFailing -> decelerate.fromSteering;
	c5 : feature status.isFailing -> decelerate.fromStatus;
end Aircraft.basic;



system DecelerateAircraft
features
	fromElectrical : feature;
	fromHydraulic  : feature;
	fromAlert      : feature;
	fromSteering   : feature;
	fromStatus     : feature;
properties
	WBSPS::IsFunction => true;
annex EMV2 {**
	use types WBSErrorLib;
	use behavior WBSErrorLib::Twostate;

	error propagations
		fromElectrical : in propagation;
		fromHydraulic  : in propagation;
		fromAlert      : in propagation;
		fromSteering   : in propagation;
		fromStatus     : in propagation;
	end propagations;	
**};
end DecelerateAircraft;



 
system implementation DecelerateAircraft.basic
subcomponents
	DecelerateWheels  : system   WheelBrake::DecelerateWheels.basic;
	DecelerateThrottle: system   Throttle::DecelerateThrottle;
connections
	c1 : feature fromElectrical -> DecelerateWheels.fromElectrical;
	c2 : feature fromHydraulic -> DecelerateWheels.fromHydraulic;
	c3 : feature fromAlert -> DecelerateWheels.fromAlert;
	c4 : feature fromSteering -> DecelerateWheels.fromSteering;
	c5 : feature fromStatus -> DecelerateWheels.fromStatus;
annex EMV2 {**
	use types WBSErrorLib;
	use behavior WBSErrorLib::TwostateWBS;

	composite error behavior
	states
		[DecelerateWheels.Failed or DecelerateThrottle.Failed]-> Failed;
	end composite;                
 **};
end DecelerateAircraft.basic;


end aircraft;