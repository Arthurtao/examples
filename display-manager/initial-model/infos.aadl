package dm::legacy::infos
public

with dm::legacy::types;


--
--  This thread will fake a GPS simulator and outputs
--  the position of the plane. The position will then
--  be used to compute the speed of the plane
--
thread gps_simulator
features
	pos : out data port dm::legacy::types::gps_position.i;
properties
	dispatch_protocol => periodic;
	period => 200 ms;
end gps_simulator;

thread implementation gps_simulator.i
end gps_simulator.i;

thread speed_calculator
features
	pos : in data port dm::legacy::types::gps_position.i;
	speed : out data port dm::legacy::types::speed;
properties
	dispatch_protocol => periodic;
	period => 2 sec;
end speed_calculator;


thread implementation speed_calculator.i
end speed_calculator.i;

--
--  The altitude_sensor receives data from a sensor
--  and sends it to a task to transform it into
--  a suitable data to evaluate the distance
--  to the ground.
--

thread altitude_sensor
features
	alt : out data port dm::legacy::types::altitude_sensed;
end altitude_sensor;

thread implementation altitude_sensor.i
end altitude_sensor.i;

thread altitude_processing
features 
	sensed_value : in data port dm::legacy::types::altitude_sensed;
	altitude     : out data port dm::legacy::types::altitude_meter;
end altitude_processing;

thread implementation altitude_processing.i
end altitude_processing.i;

--
--  The wind speed is calculated from a sensor
--  that takes a value while a processing
--  function transforms it into a suitable speed. 
--
thread wind_processing
features
	sensed : in data port dm::legacy::types::wind_sensed;
	speed : out data port dm::legacy::types::speed;
end wind_processing;

thread implementation wind_processing.i
end wind_processing.i;

thread wind_sensor
features
	sensed : out data port dm::legacy::types::wind_sensed;
end wind_sensor;

thread implementation wind_sensor.i
end wind_sensor.i;


--
--  The process infos contains all the functions necessary
--  to process non-critical flight data information
--
process infos
features
	aircraft_speed : out data port dm::legacy::types::speed;
	wind_speed : out data port dm::legacy::types::speed;
	altitude     : out data port dm::legacy::types::altitude_meter;
end infos;

process implementation infos.i
subcomponents
	gps_sensor : thread gps_simulator.i;
	speed_processing : thread speed_calculator.i;
	altitude_sensor : thread altitude_sensor.i;
	altitude_processing : thread altitude_processing.i;
	wind_sensor : thread wind_sensor.i;
	wind_processing : thread wind_processing.i;
connections
	c0 : port gps_sensor.pos -> speed_processing.pos;
	c1 : port speed_processing.speed -> aircraft_speed;
	c2 : port altitude_sensor.alt -> altitude_processing.sensed_value;
	c3 : port altitude_processing.altitude -> altitude;
	c4 : port wind_sensor.sensed -> wind_processing.sensed;
	c5 : port wind_processing.speed -> wind_speed;
end infos.i;

end dm::legacy::infos;