package dm::legacy::integration

public

with dm::legacy::infos;
with dm::legacy::monitor;
with dm::legacy::manager;
with dm::legacy::renderer;
with dm::legacy::platform;

with arinc653;

system display_manager
end display_manager;


system implementation display_manager.i
subcomponents
	infos : process dm::legacy::infos::infos.i;
	monitor : process dm::legacy::monitor::monitor.i;
	manager : process dm::legacy::manager::manager.i;
	renderer : process dm::legacy::renderer::renderer.i;
	platform : processor dm::legacy::platform::execution_platform.i;
connections
	c0 : port infos.aircraft_speed -> manager.aircraft_speed;
	c1 : port infos.altitude -> manager.altitude;
	c2 : port infos.wind_speed -> manager.wind_speed;
	c3 : port monitor.engines_status -> manager.engines_status;
	c4 : port monitor.pitot_status -> manager.pitot_status;
	c5 : port monitor.fuel_status -> manager.fuel_status;
	c6 : port manager.page_info -> renderer.page_info;
	c7 : port manager.page_warning -> renderer.page_warning;
	c8 : port manager.page_critical -> renderer.page_critical;
properties
	actual_processor_binding => (reference (platform.part1)) applies to infos;
	actual_processor_binding => (reference (platform.part2)) applies to monitor;
	actual_processor_binding => (reference (platform.part3)) applies to manager;
	actual_processor_binding => (reference (platform.part4)) applies to renderer;	

	arinc653::dal => level_c applies to platform.part1;
	arinc653::dal => level_a applies to platform.part2;
	arinc653::dal => level_a applies to platform.part3;
	arinc653::dal => level_c applies to platform.part4;

	arinc653::module_major_frame => 40ms applies to platform;

	arinc653::module_schedule => 
	    ( [Partition => reference (platform.part1);
	      Duration  => 10 ms;
	      Periodic_Processing_Start => false;],
	      [Partition => reference (platform.part2);
	      Duration  => 10 ms;
	      Periodic_Processing_Start => false;],
	      [Partition => reference (platform.part3);
	      Duration  => 10 ms;
	      Periodic_Processing_Start => false;],
	      [Partition => reference (platform.part4);
	      Duration  => 10 ms;
	      Periodic_Processing_Start => false;]
	    ) applies to platform;
end display_manager.i;

end dm::legacy::integration;