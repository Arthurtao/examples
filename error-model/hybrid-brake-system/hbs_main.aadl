package hbs_main

public

with emv2;
with hbs_components;

system main
end main;


--
-- See error description on http://hip-hops.eu/index.php/hbs-failure-annotations/hazards
--
-- For this simplified case-study, only two hazards were considered:
-- * No braking after command (H1)
-- * Wrong value braking (H2)
--
-- An omission of braking hazard (H1) occurs when braking is omitted from 
-- both the in-wheel motor and the electromechanical brake; a value 
-- braking hazard (H2) occurs when either of the two braking devices 
-- brakes with an incorrect value. 
--

system implementation main.i
subcomponents
	commbus1 	: bus hbs_components::communication_bus.i;
	commbus2 	: bus hbs_components::communication_bus.i;
	mp 			: system hbs_components::mechanical_pedal.i;
	ep 			: system hbs_components::electronic_pedal.i;
	wnc 		: system hbs_components::wnc.i;
	epc 		: system hbs_components::emb_power_converter.i;
	ipc 		: system hbs_components::iwm_power_converter.i;
	ab  		: system hbs_components::auxiliary_battery.i;
	pb  		: system hbs_components::powertrain_battery.i;
	iwm  		: system hbs_components::iwm.i;
	emb  		: system hbs_components::emb.i;
connections
	ep_wnc1 : port ep.output1 -> wnc.input1;
	ep_wnc2 : port ep.output2 -> wnc.input2;
	mp_ep : port mp.output -> ep.mechanical_input; 
	wnc_ipc : port wnc.to_ipc -> ipc.from_wnc;
	wnc_epc : port wnc.to_epc -> epc.from_wnc; 
	ab_epc : port ab.output -> epc.from_battery; 
	epc_emb : port epc.to_emb -> emb.from_epc;
	ipc_pb : port ipc.to_battery -> pb.input; 
	pb_iwm : port pb.output -> iwm.from_battery;
	iwm_ipc : port iwm.to_ipc -> ipc.from_iwm;
	ipc_iwm : port ipc.to_iwm -> iwm.from_ipc;
properties
	Actual_Connection_Binding => (reference (commbus1), reference (commbus2)) applies to ep_wnc1;
	Actual_Connection_Binding => (reference (commbus1), reference (commbus2)) applies to ep_wnc2;
annex EMV2 {**
	use types ErrorLibrary;
	use behavior hbs_errors::simple;

	composite error behavior 
	states
		[emb.Failed{ServiceOmission} and iwm.Failed{ServiceOmission}]-> Failed;
		[emb.Failed{ValueError} or iwm.Failed{ValueError}]-> Failed; 
	end composite;
**};
end main.i; 


end hbs_main;