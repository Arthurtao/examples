package HardwareParts
public
with EMV2;
		
	processor CPU
		features
			powersource: in feature ElectricalPower;
			networkaccess: requires bus access Network;
	annex EMV2 {**
		use types ErrorLibrary,GPSErrorLibrary;
		error propagations
			bindings: out propagation {ServiceOmission};
			powersource: in propagation {ServiceOmission};
			networkaccess: out propagation {ServiceOmission};
			networkaccess: in propagation {ServiceOmission};
		flows
			cpu_es1: error source bindings {ServiceOmission} when {CPUFailure};
			-- network fails if processor fails since it is driven by processor
			cpu_es2: error source networkaccess {ServiceOmission} when {CPUFailure};
		end propagations;
		properties
			-- these properties should be consistent with the ones in CPU_errorbehavior
			emv2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to cpu_es1.ServiceOmission;
			emv2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to cpu_es2.ServiceOmission;
	**};
	end CPU;
	
	processor CPU_errorbehavior extends CPU
	 annex EMV2 {**
		use types ErrorLibrary,GPSErrorLibrary;
	 	use behavior ErrorLibrary::FailStop;
	 	component error behavior
		events
		-- we override Failure to associate an error type with the event
		 Failure: error event {CPUFailure};
		propagations
		FailStop -[]-> bindings{ServiceOmission};
		FailStop -[]-> networkaccess{ServiceOmission};
	 	end component;
		properties
			emv2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to Failure;
	 **};
	end CPU_errorbehavior;

	
	bus Network
		features
			power: in feature ElectricalPower;
	annex EMV2 {**
		use types ErrorLibrary,GPSErrorLibrary;
		use behavior ErrorLibrary::FailStop;
		error propagations
		bindings: out propagation {ServiceOmission};
		power: in propagation {ServiceOmission};
		access: in propagation {ServiceOmission};
		access: out propagation {ServiceOmission};
		flows
			cpu_es: error path access{ServiceOmission} -> bindings {ServiceOmission} ;
			power_es: error path power{ServiceOmission} -> bindings {ServiceOmission} ;
			own_es: error source bindings {ServiceOmission} when {NetworkFailure};
		end propagations;
		properties
			emv2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to Failure;
	**};
	end Network;

	device PowerSupply
	features
		power: out feature;
	annex EMV2 {**
		use types ErrorLibrary,GPSErrorLibrary;
		use behavior ErrorLibrary::FailStop;
		error propagations
			power: out propagation {ServiceOmission};
		flows
			cpu_es: error source power {ServiceOmission} when {PowerSupplyFailure};
		end propagations;
	 	component error behavior
		events
		-- we override Failure to associate an error type with the event
		 	Failure: error event {PowerSupplyFailure};
		propagations
			FailStop -[]-> power{ServiceOmission};
	 	end component;
		properties
			emv2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to cpu_es.ServiceOmission;
			emv2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to Failure;
			
	**};
	end PowerSupply;
	
	-- represents electrical power
	abstract ElectricalPower
	end ElectricalPower;
	
end HardwareParts;
