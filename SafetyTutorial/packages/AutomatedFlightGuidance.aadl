package AutomatedFlightGuidance
public
with HardwareParts;
with EMV2;

-- Flight guidance is an abstract system 
-- We may later decide to turn it into a software component that needs to bound to a processor.
-- We represents its failure potential by a error behavior state machine with
-- an occurrence probability on the error event.
abstract FlightGuidance
features
InPort: in data port;
OutPort: out data port;
annex emv2 {**                                                                                                                                                                                                                                                                                                         
	use types AFGErrorLibrary;
  use behavior ErrorLibrary::FailStop;        
	error propagations
	OutPort: out propagation {NoService};
	end propagations;
	
	component error behavior
    propagations  
       FailStop-[]->OutPort{NoService};
   end component;
	properties
		emv2::OccurrenceDistribution => [ ProbabilityValue => 4.5e-5 ; Distribution => Poisson;] applies to Failure;
   **};
end FlightGuidance;

-- Autopilot is an abstract system 
-- We may later decide to turn it into a software component that needs to bound to a processor.
-- We represents its failure potential by a error behavior state machine with
-- an occurrence probability on the error event.
abstract AutoPilot
features
InPort: in data port;
OutPort: out data port;
annex emv2 {**
	use types AFGErrorLibrary;
  use behavior ErrorLibrary::FailStop;        
	error propagations
	InPort: in propagation {NoValue};
	OutPort: out propagation {NoValue};
	end propagations;
	
component error behavior
    propagations  
      FailStop-[]->Outport{NoValue};
      Operational -[Inport{NoValue}]-> OutPort{NoValue};
   end component; 
	properties
		emv2::OccurrenceDistribution => [ ProbabilityValue => 6.5e-5 ; Distribution => Poisson;] applies to Failure;
	**};
end AutoPilot;

-- The voter will be used in a configuration that replicates the flight guidance Autopilot pair 
-- the idea is to illustrate replicated channels and their allocation
-- to a two processor or three processor platform, which affects their reliability figures.
abstract AFGVoter
features
FromAP1Port: in data port;
FromAP2Port: in data port;
OutPort: out data port;
annex emv2 {**
	use types AFGErrorLibrary;
  use behavior ErrorLibrary::FailStop; 
	error propagations
	FromAP1Port: in propagation {NoValue};
	FromAP2Port: in propagation {NoValue};
	OutPort: out propagation {NoValue};
	end propagations;
	
component error behavior
    propagations  
      FailStop-[]->Outport{NoValue};
      Operational -[FromAP1Port{NoValue} and FromAP2Port{NoValue}]-> OutPort{NoValue};
  end component;
	**};
end AFGVoter;


-- This is the system that takes GPS input and provides automated flight guidance.
-- In a manual  mode the pilot flies the aircraft directly.
-- thus, auto flight guidance operation can have a lower reliability target, since the
-- pilot is typically considered to be a reliable backup.
system AutomatedFlightGuidance
features
position : in data port;
operatorCommand: in data port;
AFGOutput: out data port;
powersupply : in feature ;
annex emv2 {**                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                   
	use types AFGErrorLibrary;
  use behavior ErrorLibrary::FailStop;
	error propagations
	position: in propagation {NoValue};
	operatorCommand: in propagation {NoValue};
	AFGOutput : out propagation {NoValue};
	powersupply: in propagation {NoService};
	flows
	esource: error source AFGOutput{NoValue} ;
	-- fault tree to reflect FGS as error source.
	ePath: error path position{NoValue} -> AFGOutput{NoValue};
	epPower: error path powersupply{NoService} -> AFGOutput{NoValue};
	end propagations;
	properties
	-- We specify the failure rate of AFG as the error source.
	-- external impact is accounted for separately.
	emv2::OccurrenceDistribution => [ ProbabilityValue => 1.0e-9 ; Distribution => Poisson;] applies to esource.Novalue;
	**};
end AutomatedFlightGuidance;

-- This is a functional implementation 
system implementation AutomatedFlightGuidance.functional
subcomponents
AutoPilot: system AutoPilot;
FlightGuidance: system FlightGuidance;
connections
AFGtoFG: port position -> FlightGuidance.inport;
FGtoAP: port FlightGuidance.outport -> AutoPilot.inport;
APtoAFG: port AutoPilot.outport -> AFGOutput;
annex emv2 {**                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              
	use types AFGErrorLibrary;
  use behavior ErrorLibrary::FailStop;        
	error propagations
	bindings: out propagation {NoService};
	flows
	esource: error source bindings{NoService} when FailStop;
	end propagations;
	
component error behavior
    propagations  
      FailStop-[]->bindings{NoService};
   end component;**};
end AutomatedFlightGuidance.functional;

-- to come: an implementation with a hardware platform
-- and a dual channel implementation.
-- This is to show the same points as the DualFGS example.

end AutomatedFlightGuidance;