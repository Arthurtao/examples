package train_version1

public

with EMV2;
with Data_Model;

data speed
end speed;

data boolean
properties
	Data_Model::Data_Representation => boolean;
end boolean;

 

system door_controller
features
	speed 		: in data port speed;
	intransit   : in data port boolean;
	alarm       : in data port boolean;
	door_sensor : in data port boolean;
annex behavior_specification {**
	states
		Doors_Closed : initial state;
		Doors_Opened : state;
	transitions
		t1 : Doors_Closed -[speed? = 0 and intransit? = false ]-> Doors_Opened;
		t2 : Doors_Closed -[alarm? = true]-> Doors_Opened;
		t5 : Doors_Opened -[speed? = 0 and intransit? = false and door_sensor?= false and alarm? = false]-> Doors_Closed;
**};
end door_controller;


device alarm
features
	alert   : out data port boolean;
	reset   : in event port;
annex behavior_specification {**
	states
		Inactive : initial state;
		Active   : state;
	transitions
		t1 : Inactive 	-[self.hazard_detected	]-> Active;
		t2 : Active 	-[reset					]-> Inactive;
		t3 : Active 	-[						]-> Active {alert := true};
**};
end alarm;

system train_controller
features
	speed 		: out data port speed;
	intransit 	: out data port boolean;
annex EMV2 {**
	use types ErrorLibrary;
	use behavior train_errors::simple;
	
	error propagations
		speed : out propagation{ValueError,ServiceError};
	flows
		speed : error source speed{ValueError,ServiceError};
	end propagations;
	
	component error behavior
	events
		Failure : error event;
	transitions
		t0 : Operational -[Failure]-> Failed;
	propagations
		p0 : Failed -[]-> speed{ValueError};
		p1 : Failed -[]-> speed{ServiceError};
	end component;
**};
end train_controller;

device door_sensor
features
	value : out data port boolean;
end door_sensor;

system train

end train;

system implementation train.i1
subcomponents
	alarm 		: device alarm;
	train_ctrl  : system train_controller;
	door_ctrl  	: system door_controller;
	door_sensor : device door_sensor;
connections
	conn_sensor : port door_sensor.value 		-> door_ctrl.door_sensor;
	conn_alarm  : port alarm.alert 				-> door_ctrl.alarm;
	conn_speed  : port train_ctrl.speed 		-> door_ctrl.speed;
	conn_transit: port train_ctrl.intransit 	-> door_ctrl.intransit;
end train.i1;

end train_version1;