package speed_regulation::devices


public

with speed_regulation::icd;
with emv2;
with arp4761;
 
-----------------------------------
--  Camera and Object Detection  --
-----------------------------------
   
device camera
features
	picture : out data port speed_regulation::icd::picture;
flows
	f0 : flow source picture;
properties 
	Period => 10ms;
annex EMV2 {**
	use types speed_regulation::error_library;

	error propagations
		picture : out propagation {NoValue};
	flows
		f0 : error source picture{NoValue};
	end propagations;

	properties
		emv2::severity => ARP4761::Major applies to f0;
		emv2::likelihood => ARP4761::Probable applies to f0;
		emv2::hazards => 
			([	crossreference => "TBD";
				failure => "";
				phases => ("all");
				description => "No picture from the camera";
				comment => "Would impact the detection of obstacle";
			])
			applies to f0;
**};	
end camera;

device radar
features
	distance_estimate : out data port speed_regulation::icd::distance;
flows
	f0 : flow source distance_estimate;
properties 
	Period => 10ms;
annex EMV2 {**
	use types speed_regulation::error_library;

	error propagations
		distance_estimate : out propagation {NoValue};
	flows
		f0 : error source distance_estimate{NoValue};
	end propagations;

	properties
		emv2::severity => ARP4761::Major applies to f0;
		emv2::likelihood => ARP4761::Probable applies to f0;
		emv2::hazards => 
			([	crossreference => "TBD";
				failure => "";
				phases => ("all");
				description => "No picture from the camera";
				comment => "Would impact the detection of obstacle";
			])
			applies to f0;
**};
end radar;
 
---------------------
--  Speed Sensors  --
---------------------

device speed_sensor
features
	speed : out data port speed_regulation::icd::speed;
flows
	f0 : flow source speed;
end speed_sensor; 

device speed_wheel_sensor extends speed_sensor
properties
	Period => 10ms;
end speed_wheel_sensor;

device speed_laser_sensor extends speed_sensor
properties
	Period => 10ms;
end speed_laser_sensor;

------------------
--  GPS device  --
------------------

device gps
features
	position : out data port speed_regulation::icd::gps_position;
flows
	f0 : flow source position;
end gps;

device implementation gps.impl1
properties
	Period => 10ms;
end gps.impl1;

device implementation gps.impl2
properties
	Period => 15ms;
end gps.impl2;

----------------------
--  Warning Device  --
----------------------

device warning_device
features
	warning : in event data port speed_regulation::icd::boolean;
flows
	f0 : flow sink warning;
properties
	Period => 500ms;
end warning_device;

-------------
--  Brake  --
-------------

device brake
features
	cmd : in event data port speed_regulation::icd::speed_command.i;
flows
	f0 : flow sink cmd;
properties
	Period => 2ms;
end brake;

device acceleration
features
	cmd : in event data port speed_regulation::icd::speed_command.i;
flows
	f0 : flow sink cmd;
properties
	Period => 2ms;
end acceleration;

end speed_regulation::devices;