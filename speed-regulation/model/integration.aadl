package speed_regulation::integration

public

with speed_regulation::devices;
with speed_regulation::software;


system integration
end integration;

system implementation integration.generic
subcomponents
-- input devices
	obstacle_camera : device speed_regulation::devices::camera;
	obstacle_radar : device speed_regulation::devices::radar;
	wheel_sensor : device speed_regulation::devices::speed_wheel_sensor;
	laser_sensor : device speed_regulation::devices::speed_laser_sensor;
	gps1 : device speed_regulation::devices::gps.impl1;
	gps2 : device speed_regulation::devices::gps.impl2;
	
-- software
    image_acquisition : process speed_regulation::software::image_acquisition.i;
    radar_acquisition : process speed_regulation::software::radar_acquisition.i;
    
    speed_estimate    : process speed_regulation::software::speed_estimate.i;
    
    position_voter    : process speed_regulation::software::position_voter.i;
    
    obstacle_detection : process speed_regulation::software::obstacle_detection.i;
    
    obstacle_distance_evaluation : process speed_regulation::software::obstacle_distance_evaluation.i;
    
    speed_threshold_calculation : process speed_regulation::software::speed_threshold_computation.i;
    
    speed_controller : process speed_regulation::software::speed_controller.i;
    
    emergency_detection : process speed_regulation::software::emergency_detection.i;
    
    warning_activation : process speed_regulation::software::warning_activation.i;
	
-- output devices
	warning_alert : device speed_regulation::devices::warning_device;
	brake         : device speed_regulation::devices::brake;
	acceleration  : device speed_regulation::devices::acceleration;
connections
-- obstacle camera to image acquisition
   c000 : port obstacle_camera.picture -> image_acquisition.picture;

-- obstacle radar to radar acquisition
   c010 : port obstacle_radar.distance_estimate -> radar_acquisition.obstacle_distance;

-- speed wheel sensor to speed estimate
   c020 : port wheel_sensor.speed -> speed_estimate.wheel_sensor;

-- laser sensor to speed estimate
   c030 : port laser_sensor.speed -> speed_estimate.laser_sensor;

-- GPS1 to GPS voter
	c040 : port gps1.position -> position_voter.position1;
	
-- GPS2 to GPS voter
	c050 : port gps2.position -> position_voter.position2;
	
-- Image acquisition to obstacle detection
   c060 : port image_acquisition.obstacle_detected -> obstacle_detection.camera;

-- Radar acquisition to obstacle detection
   c070 : port radar_acquisition.obstacle_detected -> obstacle_detection.radar;

-- obstacle detection to obstacle distance evaluation
   c080 : port obstacle_detection.obstacle_detected -> obstacle_distance_evaluation.obstacle_detected;

-- obstacle distance evaluation to emergency detection
   c090 : port obstacle_distance_evaluation.obstacle_distance -> emergency_detection.obstacle_distance;

-- emergency detection to warning activation
   c100 : port emergency_detection.emergency_detected -> warning_activation.emergency_detected;
   
-- warning activation to warning device
   c110 : port warning_activation.activate_warning -> warning_alert.warning;
   
-- speed estimate to speed threshold computation

   c120 : port speed_estimate.speed -> speed_threshold_calculation.current_speed;

-- speed estimate to speed controller
	c130 : port speed_estimate.speed -> speed_controller.current_speed;

-- speed estimate to obstacle distance evaluation
	c140 : port speed_estimate.speed -> obstacle_distance_evaluation.current_speed;

-- speed estimate to emergency detection
	c150 : port speed_estimate.speed -> emergency_detection.current_speed;
	
-- emergency detection to speed controller
	c160 : port emergency_detection.emergency_detected -> speed_controller.emergency_detected;
	
-- speed threshold computation to speed controller
	c170 : port speed_threshold_calculation.threshold -> speed_controller.threshold;
	
-- position voter to speed controller
	c180 : port position_voter.position -> speed_controller.position;
	
-- position voter to speed threshold computation
	c190 : port position_voter.position -> speed_threshold_calculation.position;
	
-- speed controller to brake
	c200 : port speed_controller.cmd -> brake.cmd;
	
-- speed controller to acceleration
	c210 : port speed_controller.cmd -> brake.cmd;

flows
    --  From the camera to the warning device
	f0 : end to end flow 	obstacle_camera.f0 -> c000 ->
							image_acquisition.f0 -> c060 ->
							obstacle_detection.f0 -> c080 ->
							obstacle_distance_evaluation.f1 -> c090 ->
							emergency_detection.f1 -> c100 ->
							warning_activation.f0 -> c110 ->
							warning_alert.f0;		
end integration.generic;


end speed_regulation::integration;