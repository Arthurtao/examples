--
--
--  Model for the AADL tutorial at ESWeek 2016.
--  See details at https://wiki.sei.cmu.edu/aadl/index.php/ESWeek_Tutorial
--
--  Copyright Carnegie Mellon Software Engineering Institute, 2016.
--

package emsoft2016::software

public


with emsoft2016::icd; 
with sei;
with emsoft2016::platform;

-------------------------
--  Image Acquisition  --
-------------------------

process image_acquisition
features
	picture           : in data port emsoft2016::icd::picture;
	obstacle_detected : out data port emsoft2016::icd::obstacle_position.i;
flows
	f0 : flow path picture -> obstacle_detected;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		picture : in propagation {ItemOmission};
		obstacle_detected : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path picture{ItemOmission} -> obstacle_detected{ItemOmission};
		ef1 : error path picture{ItemOmission} -> obstacle_detected{OutOfRange};
		ef2 : error path processor{ServiceError} -> obstacle_detected{ItemOmission};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> obstacle_detected{ItemOmission};
	end component;
**};
end image_acquisition;

process implementation image_acquisition.i
subcomponents
	thr_acq : thread image_acquisition_thr.i;
connections
	c0 : port picture -> thr_acq.picture;
	c1 : port thr_acq.obstacle_detected -> obstacle_detected;
flows
	f0 : flow path picture -> c0 -> thr_acq.f0 -> c1 -> obstacle_detected;
end image_acquisition.i;

thread image_acquisition_thr
features
	picture           : in data port emsoft2016::icd::picture;
	obstacle_detected : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path picture -> obstacle_detected;
properties
	sei::mipsbudget   => 25.0 MIPS;
	Dispatch_Protocol => Periodic;
	Period            => 50ms;
	compute_execution_time => 10 ms .. 40 ms;
	reference_processor => classifier (emsoft2016::platform::ecu);
	sei::instructionsperdispatch => 1.24 kipd .. 1.25 mipd; 
end image_acquisition_thr;

thread implementation image_acquisition_thr.i
end image_acquisition_thr.i;

-------------------
--  Speed Voter  --
-------------------

process speed_voter
features
	wheel_sensor : in data port emsoft2016::icd::speed;
	laser_sensor : in data port emsoft2016::icd::speed;
	speed        : out data port emsoft2016::icd::speed;
flows
	f0 : flow path wheel_sensor -> speed;
	f1 : flow path laser_sensor -> speed;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		wheel_sensor : in propagation {ItemOmission,OutOfRange};
		laser_sensor : in propagation {ItemOmission,OutOfRange};
		speed : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path wheel_sensor{ItemOmission} -> speed{ItemOmission};
		ef1 : error path wheel_sensor{OutOfRange} -> speed{OutOfRange};
		ef2 : error path laser_sensor{ItemOmission} -> speed{ItemOmission};
		ef3 : error path laser_sensor{OutOfRange} -> speed{OutOfRange};
		ef4 : error path processor{ServiceError} -> speed{ItemOmission};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> speed{ItemOmission};
	end component;
**};
end speed_voter;

process implementation speed_voter.i
subcomponents
	thr : thread speed_voter_thr.i;
connections
	c0 : port wheel_sensor -> thr.wheel_sensor;
	c1 : port laser_sensor -> thr.laser_sensor;
	c2 : port thr.speed -> speed;
flows
	f0 : flow path wheel_sensor -> c0 -> thr.f0 -> c2 -> speed;
	f1 : flow path laser_sensor -> c1 -> thr.f1 -> c2 -> speed;
end speed_voter.i;

thread speed_voter_thr
features
	wheel_sensor : in data port emsoft2016::icd::speed;
	laser_sensor : in data port emsoft2016::icd::speed;
	speed        : out data port emsoft2016::icd::speed;
flows
	f0 : flow path wheel_sensor -> speed;
	f1 : flow path laser_sensor -> speed;
properties
	Dispatch_Protocol => Periodic;
	Period            => 8ms;
	sei::mipsbudget   => 8.0 MIPS;
end speed_voter_thr;

thread implementation speed_voter_thr.i
end speed_voter_thr.i;

--------------------------
--  Obstacle Detection  --
--------------------------

process obstacle_detection
features
	camera : in data port emsoft2016::icd::boolean;
	radar : in data port emsoft2016::icd::boolean;
	obstacle_detected : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path camera -> obstacle_detected;
	f1 : flow path radar -> obstacle_detected;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		camera : in propagation {ItemOmission,OutOfRange};
		radar : in propagation {ItemOmission,OutOfRange};
		obstacle_detected : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path camera{ItemOmission} -> obstacle_detected{ItemOmission};
		ef1 : error path radar{ItemOmission} -> obstacle_detected{ItemOmission};
		ef2 : error path camera{ItemOmission} -> obstacle_detected{OutOfRange};
		ef3 : error path radar{ItemOmission} -> obstacle_detected{OutOfRange};
		ef4 : error path processor{ServiceError} -> obstacle_detected{ItemOmission};
		ef5 : error path camera{OutOfRange} -> obstacle_detected{OutOfRange};
		ef6 : error path radar{OutOfRange} -> obstacle_detected{OutOfRange};
		ef7 : error path processor{ServiceError} -> obstacle_detected{ItemOmission};

	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> obstacle_detected{ItemOmission};
	end component;
**};
end obstacle_detection;

process implementation obstacle_detection.i
subcomponents
	thr : thread obstacle_detection_thr;
connections
	c0 : port camera -> thr.camera;
	c1 : port radar -> thr.radar;
	c2 : port thr.obstacle_detected -> obstacle_detected;
flows
	f0 : flow path camera -> c0 -> thr.f0 -> c2 -> obstacle_detected;
	f1 : flow path radar -> c1 -> thr.f1 -> c2 -> obstacle_detected;
end obstacle_detection.i;

thread obstacle_detection_thr
features
	camera : in data port emsoft2016::icd::boolean;
	radar : in data port emsoft2016::icd::boolean;
	obstacle_detected : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path camera -> obstacle_detected;
	f1 : flow path radar -> obstacle_detected;
properties
	Dispatch_Protocol => Periodic;
	Period            => 100ms;
	Compute_Execution_Time => 20 ms .. 50 ms; 
	sei::mipsbudget   => 10.0 MIPS; 
	sei::instructionsperdispatch => 0.9 kipd .. 0.98 mipd; 
	reference_processor => classifier (emsoft2016::platform::ecu);
end obstacle_detection_thr;

------------------------------------
--  Obstacle Distance Evaluation  --
------------------------------------

process obstacle_distance_evaluation
features
	current_speed : in data port emsoft2016::icd::speed;
	obstacle_detected : in data port emsoft2016::icd::boolean;
	obstacle_distance : out data port emsoft2016::icd::distance;
flows
	f0 : flow path current_speed -> obstacle_distance;
	f1 : flow path obstacle_detected -> obstacle_distance;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		current_speed : in propagation {ItemOmission,OutOfRange};
		obstacle_detected : in propagation {ItemOmission,OutOfRange};
		obstacle_distance : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path current_speed{ItemOmission} -> obstacle_distance{ItemOmission};
		ef1 : error path obstacle_detected{ItemOmission} -> obstacle_distance{ItemOmission};
		ef2 : error path current_speed{OutOfRange} -> obstacle_distance{OutOfRange};
		ef3 : error path obstacle_detected{OutOfRange} -> obstacle_distance{OutOfRange};
		ef4 : error path processor{ServiceError} -> obstacle_distance{ItemOmission};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> obstacle_distance{ItemOmission};
	end component;
**};
end obstacle_distance_evaluation;

process implementation obstacle_distance_evaluation.i
subcomponents
	thr : thread obstacle_distance_evaluation_thr;
connections
	c0 : port current_speed -> thr.current_speed;
	c1 : port obstacle_detected -> thr.obstacle_detected;
	c2 : port thr.obstacle_distance -> obstacle_distance;
flows
	f0 : flow path current_speed -> c0 -> thr.f0 -> c2 -> obstacle_distance;
	f1 : flow path obstacle_detected -> c1 -> thr.f1 -> c2 -> obstacle_distance;
end obstacle_distance_evaluation.i;

thread obstacle_distance_evaluation_thr
features
	current_speed : in data port emsoft2016::icd::speed;
	obstacle_detected : in data port emsoft2016::icd::boolean;
	obstacle_distance : out data port emsoft2016::icd::distance;
flows
	f0 : flow path current_speed -> obstacle_distance;
	f1 : flow path obstacle_detected -> obstacle_distance;
properties
	Dispatch_Protocol => Periodic;
	Period            => 10ms;
	sei::mipsbudget   => 8.0 MIPS;
end obstacle_distance_evaluation_thr;

---------------------------
--  Emergency Detection  --
---------------------------

process emergency_detection
features
	current_speed : in data port emsoft2016::icd::speed;
	obstacle_distance : in data port emsoft2016::icd::distance;
	emergency_detected : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path current_speed -> emergency_detected;
	f1 : flow path obstacle_distance -> emergency_detected;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		current_speed : in propagation {ItemOmission,OutOfRange};
		obstacle_distance : in propagation {ItemOmission,OutOfRange};
		emergency_detected : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path current_speed{ItemOmission} -> emergency_detected{ItemOmission};
		ef1 : error path obstacle_distance{ItemOmission} -> emergency_detected{ItemOmission};
		ef2 : error path current_speed{ItemOmission} -> emergency_detected{OutOfRange};
		ef3 : error path obstacle_distance{ItemOmission} -> emergency_detected{OutOfRange};
		ef4 : error path processor{ServiceError} -> emergency_detected{ItemOmission};
		ef5 : error path current_speed{OutOfRange} -> emergency_detected{OutOfRange};
		ef6 : error path obstacle_distance{OutOfRange} -> emergency_detected{OutOfRange};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> emergency_detected{ItemOmission};
	end component;
**};
end emergency_detection;

process implementation emergency_detection.i
subcomponents
	thr : thread emergency_detection_thr;
connections
	c0 : port current_speed -> thr.current_speed;
	c1 : port obstacle_distance -> thr.obstacle_distance;
	c2 : port thr.emergency_detected -> emergency_detected;
flows 
	f0 : flow path current_speed -> c0 -> thr.f0 -> c2 -> emergency_detected;
	f1 : flow path obstacle_distance -> c1 -> thr.f1 -> c2 -> emergency_detected;
end emergency_detection.i;

thread emergency_detection_thr
features
	current_speed : in data port emsoft2016::icd::speed;
	obstacle_distance : in data port emsoft2016::icd::distance;
	emergency_detected : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path current_speed -> emergency_detected;
	f1 : flow path obstacle_distance -> emergency_detected;
properties
	Dispatch_Protocol => Periodic;
	Period            => 4ms;
	sei::mipsbudget   => 5.0 MIPS;
end emergency_detection_thr;

--------------------------
--  Warning Activation  --
--------------------------

process warning_activation
features
	emergency_detected : in data port emsoft2016::icd::boolean;
	activate_warning   : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path emergency_detected -> activate_warning;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		emergency_detected : in propagation {ItemOmission,OutOfRange};
		activate_warning : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path emergency_detected{ItemOmission} -> activate_warning{ItemOmission};
		ef1 : error path emergency_detected{OutOfRange} -> activate_warning{OutOfRange};
		ef4 : error path processor{ServiceError} -> activate_warning{ItemOmission};
		ef5 : error path processor{ServiceError} -> activate_warning{ItemOmission};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> activate_warning{ItemOmission};
	end component;
**};
end warning_activation;

process implementation warning_activation.i
subcomponents
	thr : thread warning_activation_thr;
connections
	c0 : port emergency_detected -> thr.emergency_detected;
	c1 : port thr.activate_warning -> activate_warning;
flows
	f0 : flow path emergency_detected -> c0 -> thr.f0 -> c1 -> activate_warning;
end warning_activation.i;

thread warning_activation_thr
features
	emergency_detected : in data port emsoft2016::icd::boolean;
	activate_warning   : out data port emsoft2016::icd::boolean;
flows
	f0 : flow path emergency_detected -> activate_warning;
properties
	Dispatch_Protocol => Periodic;
	Period            => 2ms;
	sei::mipsbudget   => 5.0 MIPS;
end warning_activation_thr;



------------------------
--  Speed Controller  --
------------------------

process speed_controller
features
	obstacle_position : in data port emsoft2016::icd::obstacle_position.i;
	current_speed     : in data port emsoft2016::icd::speed;
	desired_speed     : in data port emsoft2016::icd::speed;
	brake_cmd         : out data port emsoft2016::icd::brake_cmd;
	speed_cmd         : out data port emsoft2016::icd::speed_cmd;
flows
	f10 : flow path obstacle_position -> brake_cmd;
	f11 : flow path current_speed -> brake_cmd;
	f12 : flow path desired_speed -> brake_cmd;
	
	f00 : flow path obstacle_position -> speed_cmd;
	f01 : flow path current_speed -> speed_cmd;
	f02 : flow path desired_speed -> speed_cmd;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		obstacle_position : in propagation {ItemOmission,OutOfRange};
		current_speed : in propagation {ItemOmission,OutOfRange};
		desired_speed : in propagation {OutOfRange};
		brake_cmd : out propagation {ItemOmission,OutOfRange};
		speed_cmd : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef00 : error path obstacle_position{ItemOmission}  -> brake_cmd{ItemOmission};
		ef01 : error path obstacle_position{OutOfRange}    -> brake_cmd{OutOfRange};
		ef02 : error path current_speed{ItemOmission}      -> brake_cmd{ItemOmission};
		ef03 : error path current_speed{OutOfRange}        -> brake_cmd{OutOfRange};
		ef05 : error path desired_speed{OutOfRange}        -> brake_cmd{OutOfRange};
		ef06 : error path processor{ServiceError}          -> brake_cmd{ItemOmission};
		
		ef10 : error path obstacle_position{ItemOmission}  -> speed_cmd{ItemOmission};
		ef11 : error path obstacle_position{OutOfRange}    -> speed_cmd{OutOfRange};
		ef12 : error path current_speed{ItemOmission}      -> speed_cmd{ItemOmission};
		ef13 : error path current_speed{OutOfRange}        -> speed_cmd{OutOfRange};
		ef15 : error path desired_speed{OutOfRange}        -> speed_cmd{OutOfRange};
		ef16 : error path processor{ServiceError}          -> speed_cmd{ItemOmission};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p0 : FailStop -[]-> speed_cmd{ItemOmission};
	   	p1 : FailStop -[]-> brake_cmd{ItemOmission};
	end component;
**};
end speed_controller;

process implementation speed_controller.i
subcomponents
	accel_thr : thread speed_controller_accel_thr;
	brake_thr : thread speed_controller_brake_thr;
connections
	c00 : port obstacle_position    -> accel_thr.obstacle_position;
	c01 : port current_speed        -> accel_thr.current_speed;
	c02 : port desired_speed        -> accel_thr.desired_speed;
	c03 : port accel_thr.speed_cmd  -> speed_cmd;

	c10 : port obstacle_position    -> brake_thr.obstacle_position;
	c11 : port current_speed        -> brake_thr.current_speed;
	c12 : port desired_speed        -> brake_thr.desired_speed;
	c13 : port brake_thr.brake_cmd  -> brake_cmd;
flows
	f00 : flow path obstacle_position -> c00 -> accel_thr.f0 -> c03 -> speed_cmd;
	f01 : flow path current_speed -> c01 -> accel_thr.f1 -> c03 -> speed_cmd;
	f02 : flow path desired_speed -> c02 -> accel_thr.f2 -> c03 -> speed_cmd;
	
	f10 : flow path obstacle_position  -> c10 -> brake_thr.f0 -> c13 -> brake_cmd;
	f11 : flow path current_speed      -> c11 -> brake_thr.f1 -> c13 -> brake_cmd;
	f12 : flow path desired_speed      -> c12 -> brake_thr.f2 -> c13 -> brake_cmd;
end speed_controller.i;

thread speed_controller_brake_thr
features
	obstacle_position : in data port emsoft2016::icd::obstacle_position.i;
	current_speed     : in data port emsoft2016::icd::speed;
	desired_speed     : in data port emsoft2016::icd::speed;
	brake_cmd         : out data port emsoft2016::icd::brake_cmd;
flows
	f0 : flow path obstacle_position -> brake_cmd;
	f1 : flow path current_speed -> brake_cmd;
	f2 : flow path desired_speed -> brake_cmd;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5ms;
	sei::mipsbudget   => 5.0 MIPS;
end speed_controller_brake_thr;

thread speed_controller_accel_thr
features
	obstacle_position : in data port emsoft2016::icd::obstacle_position.i;
	current_speed     : in data port emsoft2016::icd::speed;
	desired_speed     : in data port emsoft2016::icd::speed;
	speed_cmd         : out data port emsoft2016::icd::speed_cmd;
flows
	f0 : flow path obstacle_position -> speed_cmd;
	f1 : flow path current_speed -> speed_cmd;
	f2 : flow path desired_speed -> speed_cmd;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5ms;
	sei::mipsbudget   => 5.0 MIPS;
end speed_controller_accel_thr;



--------------------------------
--  The entertainment system  --
--------------------------------

process entertainment
features
	music_in  : in data port emsoft2016::icd::music.i;
	contacts  : in data port emsoft2016::icd::contacts.i;
	infos     : out data port emsoft2016::icd::entertainment_infos;
	music_out : out data port emsoft2016::icd::music.i;
flows
	f0 : flow path music_in -> music_out;
	f1 : flow path music_in -> infos;
	f2 : flow path contacts -> infos;
end entertainment;


process implementation entertainment.i
subcomponents
   thr : thread entertainment_thr;
connections
  c0 : port music_in -> thr.music_in;
  c1 : port contacts -> thr.contacts;
  c2 : port thr.infos -> infos;
  c3 : port thr.music_out -> music_out;
end entertainment.i;

thread entertainment_thr
features
	music_in  : in data port emsoft2016::icd::music.i;
	contacts  : in data port emsoft2016::icd::contacts.i;
	infos     : out data port emsoft2016::icd::entertainment_infos;
	music_out : out data port emsoft2016::icd::music.i;
flows
	f0 : flow path music_in -> music_out;
	f1 : flow path music_in -> infos;
	f2 : flow path contacts -> infos;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5ms;
	sei::mipsbudget   => 5.0 MIPS;
end entertainment_thr;


end emsoft2016::software;
  