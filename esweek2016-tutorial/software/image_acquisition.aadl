package emsoft2016::software::image_acquisition

public

with emsoft2016::icd; 
with sei;

-------------------------
--  Image Acquisition  --
-------------------------

process image_acquisition
features
	picture           : in data port emsoft2016::icd::picture;
	obstacle_detected : out data port emsoft2016::icd::obstacle_position.i;
flows
	f0 : flow path picture -> obstacle_detected;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		picture : in propagation {ItemOmission};
		obstacle_detected : out propagation {ItemOmission,OutOfRange};
		processor : in propagation {ServiceError};
	flows
		ef0 : error path picture{ItemOmission} -> obstacle_detected{ItemOmission};
		ef1 : error path picture{ItemOmission} -> obstacle_detected{OutOfRange};
		ef2 : error path processor{ServiceError} -> obstacle_detected{ItemOmission};
	end propagations;

	component error behavior
	transitions
		t0 : Operational -[processor{ServiceError}]-> FailStop;
		t1 : FailStop -[processor{NoError}]-> Operational;
	propagations
	   	p1 : FailStop -[]-> obstacle_detected{ItemOmission};
	end component;
**};
end image_acquisition;

process implementation image_acquisition.i
subcomponents
	thr_acq : thread image_acquisition_thr.i;
connections
	c0 : port picture -> thr_acq.picture;
	c1 : port thr_acq.obstacle_detected -> obstacle_detected;
flows
	f0 : flow path picture -> c0 -> thr_acq.f0 -> c1 -> obstacle_detected;
end image_acquisition.i;

thread image_acquisition_thr
features
	picture           : in data port emsoft2016::icd::picture;
	obstacle_detected : out data port emsoft2016::icd::obstacle_position.i;
flows
	f0 : flow path picture -> obstacle_detected;
properties
	sei::mipsbudget   => 25.0 MIPS;
	Dispatch_Protocol => Periodic;
	Period            => 50ms;
	compute_execution_time => 10 ms .. 40 ms;
--	reference_processor => classifier (emsoft2016::platform::ecu);
	sei::instructionsperdispatch => 1.24 kipd .. 1.25 mipd; 
end image_acquisition_thr;

thread implementation image_acquisition_thr.i
end image_acquisition_thr.i;

end emsoft2016::software::image_acquisition;