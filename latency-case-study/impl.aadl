package latency_cs::impl

public

with latency_cs::functional;
with base_types;

--
-- position is the data to transport the
-- GPS coordinates between the functions
--

data position
end position;

data implementation position.i
subcomponents
	x : data base_types::unsigned_64;
	y : data base_types::unsigned_64;
end position.i;


------------------------
--  Sensing function  --
------------------------

--
-- Realization of the sensing function in software
--

process sensing extends latency_cs::functional::sensing
features
	dataout : refined to out data port position.i;
end sensing;

process implementation sensing.i
subcomponents
	ts : thread thr_sensing;
connections
	c0 : port ts.dataout -> dataout;
end sensing.i;

thread thr_sensing
features
	dataout : out data port position.i;
end thr_sensing;


---------------------------
--  Processing function  --
---------------=-----------

process processing extends latency_cs::functional::processing
features
	datain1 : refined to in data port position.i;
	datain2 : refined to in data port position.i;
	dataout : refined to out data port position.i;
flows
	f2 : flow sink datain1;
	f3 : flow sink datain2;
end processing;

process implementation processing.i
subcomponents
	tf : thread thr_filter;
	ts : thread thr_stats;
connections
	c0 : port datain1 -> tf.datain1;
	c1 : port datain2 -> tf.datain2;
	c2 : port datain1 -> ts.datain1;
	c3 : port datain2 -> ts.datain2;
	c4 : port tf.dataout -> dataout;
flows
	f0 : flow path datain1 -> c0 -> tf.f0 -> c4 -> dataout; 
	f1 : flow path datain2 -> c1 -> tf.f1 -> c4 -> dataout; 
	f2 : flow sink datain1 -> c2 -> ts.f0; 
	f3 : flow sink datain2 -> c3 -> ts.f1; 
end processing.i;

thread thr_stats
features
	datain1 : in data port position.i;
	datain2 : in data port position.i;
flows
	f0 : flow sink datain1;
	f1 : flow sink datain2;
end thr_stats;

thread thr_filter
features
	datain1 : in data port position.i;
	datain2 : in data port position.i;
	dataout : out data port position.i;
flows
	f0 : flow path datain1 -> dataout;
	f1 : flow path datain2 -> dataout;
end thr_filter;


--------------------------
--  Actuating function  --
--------------------------

--
-- Realization of the actuating function in software
--

thread thr_collect
features
	datain : in data port position.i;
	dataout : out data port position.i;
flows
	f0 : flow path datain -> dataout;
end thr_collect;

thread thr_display
features
	datain : in data port position.i;
flows
	f0 : flow sink datain;
end thr_display;

process actuating extends latency_cs::functional::actuating
features
	datain : refined to in data port position.i;
end actuating;

process implementation actuating.i
subcomponents
	tc : thread thr_collect;
	tp : thread thr_display;
connections
	c0 : port datain -> tc.datain;
	c1 : port tc.dataout -> tp.datain;
flows
	f0 : flow sink datain -> c0 -> tc.f0 -> c1 -> tp.f0;
end actuating.i;

end latency_cs::impl;