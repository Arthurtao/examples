package latency_cs::integration


public

with latency_cs::functional;
with latency_cs::impl;
with latency_cs::platform;
with arinc653;

system integration extends latency_cs::functional::integration
end integration;

system implementation integration.software_generic extends latency_cs::functional::integration.i
subcomponents
	p  		: refined to process  latency_cs::impl::processing.i {Classifier_Substitution_Rule => Type_Extension;};
	s1 		: refined to process latency_cs::impl::sensing.i {Classifier_Substitution_Rule => Type_Extension;};
	s2 		: refined to process  latency_cs::impl::sensing.i {Classifier_Substitution_Rule => Type_Extension;};
	a  		: refined to process  latency_cs::impl::actuating.i {Classifier_Substitution_Rule => Type_Extension;};
end integration.software_generic;


system implementation integration.software_distributed extends integration.software_generic
subcomponents
	p_cpu 	: processor latency_cs::platform::generic_cpu;
	s_p_bus: bus latency_cs::platform::generic_bus;
	p_a_bus : bus latency_cs::platform::generic_bus;
properties
	actual_processor_binding => (reference (p_cpu)) applies to p;
	actual_connection_binding => (reference (s_p_bus)) applies to c0;
	actual_connection_binding => (reference (s_p_bus)) applies to c1;
	actual_connection_binding => (reference (p_a_bus)) applies to c2;
end integration.software_distributed;


system implementation integration.software_integrated extends integration.software_generic
subcomponents
	s1 		: refined to process latency_cs::impl::sensing.i {Classifier_Substitution_Rule => Type_Extension;};
	s2 		: refined to process  latency_cs::impl::sensing.i {Classifier_Substitution_Rule => Type_Extension;};
	a  		: refined to process  latency_cs::impl::actuating.i {Classifier_Substitution_Rule => Type_Extension;};
	cpu 	: processor latency_cs::platform::ima_cpu.i;
properties
	actual_processor_binding => (reference (cpu.part1)) applies to s1;
	actual_processor_binding => (reference (cpu.part2)) applies to s2;
	actual_processor_binding => (reference (cpu.part3)) applies to p;
	actual_processor_binding => (reference (cpu.part4)) applies to a;
end integration.software_integrated;


system implementation integration.software_integrated1 extends integration.software_integrated
properties
	arinc653::module_schedule => ([Partition => reference (cpu.part1) ; Duration => 50 ms ; Periodic_Processing_Start => true;],
		                          [Partition => reference (cpu.part2) ; Duration => 50 ms ; Periodic_Processing_Start => true;],
		                          [Partition => reference (cpu.part3) ; Duration => 50 ms ; Periodic_Processing_Start => true;],
		                          [Partition => reference (cpu.part4) ; Duration => 50 ms ; Periodic_Processing_Start => true;])
		                         applies to cpu;
end integration.software_integrated1;


system implementation integration.software_integrated2 extends integration.software_integrated
properties
	arinc653::module_schedule => ([Partition => reference (cpu.part3) ; Duration => 50 ms ; Periodic_Processing_Start => true;],
		                          [Partition => reference (cpu.part2) ; Duration => 50 ms ; Periodic_Processing_Start => true;],
		                          [Partition => reference (cpu.part4) ; Duration => 50 ms ; Periodic_Processing_Start => true;],
		                          [Partition => reference (cpu.part1) ; Duration => 50 ms ; Periodic_Processing_Start => true;])
		                         applies to cpu;
end integration.software_integrated2;

end latency_cs::integration;


